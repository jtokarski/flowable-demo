
plugins {
    id 'java'
    alias libs.plugins.springDependencyManagementPlugin
    alias libs.plugins.springBootPlugin
}

group = 'org.defendev.demo'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${libs.versions.springCloudVersion.get()}"
    }
}

dependencies {
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-webflux'
    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-gateway-server-webflux'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-security'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-oauth2-client'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-thymeleaf'
    implementation group: 'org.thymeleaf.extras', name: 'thymeleaf-extras-springsecurity6'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-log4j2'
    implementation libs.apacheCommonsLang3
    implementation libs.apacheCommonsCollections4
    implementation libs.guava
    implementation libs.assertj
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    testRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-launcher'
    modules {
        /* See:
         *   https://docs.spring.io/spring-boot/how-to/logging.html#howto.logging.log4j
         */
        module("org.springframework.boot:spring-boot-starter-logging") {
            replacedBy("org.springframework.boot:spring-boot-starter-log4j2")
        }
    }
}

tasks.named('test', org.gradle.api.tasks.testing.Test) {
    useJUnitPlatform()
}
